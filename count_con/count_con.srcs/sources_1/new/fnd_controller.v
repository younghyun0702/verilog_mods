`timescale 1ns / 1ps

module fnd_controller (
    input        clock,
    input        reset,
    input  [7:0] fnd_in,
    output [3:0] fnd_com,
    output [7:0] fnd_data
);

    wire [3:0] w_out_mux;
    wire [3:0] w_digit_1, w_digit_10, w_digit_100, w_digit_1000;
    wire       w_clk_1khz;
    wire [1:0] w_digit_sel;

    digit_splitter U_DIGIT_SPLIT (
        .digit_in(fnd_in),
        .digit_1(w_digit_1),
        .digit_10(w_digit_10),
        .digit_100(w_digit_100),
        .digit_1000(w_digit_1000)
    );

    mux_4x1 U_MUX_4x1 (
        .input0 (w_digit_1),     // digit 1
        .input1 (w_digit_10),    // digit 10
        .input2 (w_digit_100),   // digit 100
        .input3 (w_digit_1000),  // digit 1000
        .sel    (w_digit_sel),
        .out_mux(w_out_mux)      // to BCD
    );

    bcd U_BCD (
        .fnd_in  (w_out_mux),
        .bcd_data(fnd_data)
    );

    clk_div_1khz U_CLK_DIV_KHZ (
        .clock (clock),
        .reset (reset),
        .o_1khz(w_clk_1khz)
    );

    counter_4 U_COUNTER_4 (
        .clock(w_clk_1khz),
        .reset(reset),
        .digit_sel(w_digit_sel)
    );

    decoder_2x4 U_DECODER_2X4 (
        .decoder_in(w_digit_sel),
        .fnd_com   (fnd_com)
    );
endmodule


module clk_div_1khz (
    input  clock,
    input  reset,
    output o_1khz
);

    reg [15:0] counter_reg;
    reg o_1khz_reg;

    assign o_1khz = o_1khz_reg;  // 초기화가 필요

    always @(posedge clock, posedge reset) begin
        if (reset) begin
            counter_reg <= 16'd0;
            o_1khz_reg  <= 1'b0;
        end else if (counter_reg == (50_000 - 1)) begin
            counter_reg <= 16'd0;
            o_1khz_reg  <= ~o_1khz_reg;
        end else begin
            counter_reg <= counter_reg + 1;
        end
    end
endmodule

module counter_4 (
    input clock,
    input reset,
    output [1:0] digit_sel
);

    reg [1:0] counter_reg;
    assign digit_sel = counter_reg;

    always @(posedge clock, posedge reset) begin
        if (reset) begin
            counter_reg <= 0;
        end else begin
            counter_reg <= counter_reg + 1;
        end
    end

endmodule

module decoder_2x4 (
    input      [1:0] decoder_in,
    output reg [3:0] fnd_com
);

    always @(*) begin
        case (decoder_in)
            2'b00:   fnd_com = 4'b1110;
            2'b01:   fnd_com = 4'b1101;
            2'b10:   fnd_com = 4'b1011;
            2'b11:   fnd_com = 4'b0111;
            default: fnd_com = 4'b1111;
        endcase
    end
endmodule

module digit_splitter (
    input  [7:0] digit_in,
    output [3:0] digit_1,
    output [3:0] digit_10,
    output [3:0] digit_100,
    output [3:0] digit_1000
);

    assign digit_1    = digit_in % 10;
    assign digit_10   = (digit_in / 10) % 10;
    assign digit_100  = (digit_in / 100) % 10;
    assign digit_1000 = (digit_in / 1000) % 10;

endmodule

module mux_4x1 (
    input  [3:0] input0,  // digit 1
    input  [3:0] input1,  // digit 10
    input  [3:0] input2,  // digit 100
    input  [3:0] input3,  // digit 1000
    input  [1:0] sel,     // to select input
    output [3:0] out_mux  // to BCD 
);

    reg [3:0] out_reg;
    assign out_mux = out_reg;

    // mux, (*) all input: sensitivity list
    always @(*  /*input0, input1, input2, input3, sel*/) begin
        case (sel)
            2'b00:   out_reg = input0;
            2'b01:   out_reg = input1;
            2'b10:   out_reg = input2;
            2'b11:   out_reg = input3;
            default: out_reg = 4'b0000;
        endcase
    end
endmodule

module bcd (
    input  wire [3:0] fnd_in,
    output reg  [7:0] bcd_data
);

    always @(fnd_in) begin  // 동작을 기술하는 것 assign
        case (fnd_in)
            4'b0000: bcd_data = 8'hC0;
            4'b0001: bcd_data = 8'hF9;
            4'b0010: bcd_data = 8'hA4;
            4'b0011: bcd_data = 8'hB0;
            4'b0100: bcd_data = 8'h99;

            4'b0101: bcd_data = 8'h92;
            4'b0110: bcd_data = 8'h82;
            4'b0111: bcd_data = 8'hF8;
            4'b1000: bcd_data = 8'h80;
            4'b1001: bcd_data = 8'h90;

            4'b1010: bcd_data = 8'h88;  // A
            4'b1011: bcd_data = 8'h83;  // b
            4'b1100: bcd_data = 8'hC6;  // C
            4'b1101: bcd_data = 8'hA1;  // d
            4'b1110: bcd_data = 8'h86;  // E
            4'b1111: bcd_data = 8'h8E;  // F

            // default: bcd_data = 8'hxx;  // for debuging
            default: bcd_data = 8'hFF;
        endcase
    end
endmodule
